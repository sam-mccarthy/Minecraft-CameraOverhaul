def scriptDir = buildscript.sourceFile.parentFile

tasks.register("build") {
	it.dependsOn gradle.includedBuilds*.task(':build')
}

subprojects {
	// Skip folders without a 'gradle.properties' file.
	def propertiesFile = new File(project.projectDir, 'gradle.properties')
	if (!propertiesFile.exists()) {
		println "Skipping project $project.name"
		return
	}

	// Merge in root properties.
	def properties = new Properties();
	properties.load(new File(scriptDir, 'gradle.properties').newReader())
	properties.load(propertiesFile.newReader())

	// Plugins
	apply plugin: "java"

	base {
		archivesName = properties.archives_base_name
		version = "${properties.mod_version}-${properties.loader_name}-${project.name}"
		group = properties.maven_group

		//layout.buildDirectory = new File(scriptDir, "build")
		layout.buildDirectory = new File(scriptDir, "build/${properties.loader_name}-${project.name}")
	}

	// Subprojects use the same codebase, but different properties & preprocessor defines.
	sourceSets {
		main.java.srcDirs = [ new File(scriptDir, "src/main/java").absolutePath ]
		main.resources.srcDirs = [ new File(scriptDir, "src/main/resources").absolutePath ]
	}

	// Language version.
	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_1_8
	}
	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
		options.release = 17
	}

	// Shared dependencies.
	dependencies {
		annotationProcessor 'systems.manifold:manifold-preprocessor:2024.1.1'
	}

	def mcVersionSplit = properties.minecraft_version.split("\\.")
	def mcVersionMajor = mcVersionSplit[0]
	def mcVersionMain = mcVersionSplit[1]
	def mcVersionMinor = mcVersionSplit.size() >= 3 ? (mcVersionSplit[2] as String).padLeft(2, '0') : '00'
	def merged_mc_version = "$mcVersionMajor$mcVersionMain$mcVersionMinor"

	tasks.withType(JavaCompile).forEach {
		// Enable & configure manifold
		it.options.compilerArgs += [
			'-Xplugin:Manifold',
			"-AMC_VERSION=${merged_mc_version}",
			"-A${properties.loader_name.toUpperCase()}_LOADER"
		]
	}

	java.withSourcesJar()
}
